// stem_wl.ks — stem's Wayland/Hyprland windowed frontend (pure Krypton).
//
// MVP: host stem's terminal engine (PTY + VT grid) inside a k:wayland window.
// Reuses: k:wayland (surface + software framebuffer + 8x16 font + keyboard),
// stem term.k (VT/grid), stem pty.k (PTY), stem config.k (stem.conf).
//
// This is the .ks orchestration layer — the perf-critical grid/VT/PTY live in
// the imported compiled .k modules; the event loop + wiring live here.
//
// Run:  kcc -r stem_wl.ks    (needs a running Wayland compositor, e.g. Hyprland)

import "k:wayland"
import "./term.k"      // gridNew / gridFeed / gridRender
import "./pty.k"       // ptyMaster / ptySlaveName / ptyForkExec / fd*
import "./config.k"    // confLoad / confGet / confGetInt
import "./stemfont.k"  // stemFontLoad / stemDrawChar (extended glyphs)

// Find a wl_registry global's `name` by interface (registry obj=2, global op=0).
// (Local helper — k:wayland exposes the wire getters but not this scan.)
func _wlFind(b, n, iface) {
    let off = 0
    while off + 8 <= n {
        let sz = wlSize(b, off)
        if sz < 8 { emit -1 }
        if wlObject(b, off) == 2 && wlOpcode(b, off) == 0 {
            if wlReadStr(b, off + 12) == iface { emit wlU32(b, off + 8) }
        }
        off = off + sz
    }
    emit -1
}

// ── minimal keysym(keycode) -> bytes for the shell ───────────────────────────
// X11 keycodes (wlKeyToKc = evdev+8). Covers printable ASCII via a layout row
// map + the essential control keys. Full keymap/layout = future.
func kKeyBytes(kc, shift, ctrl, alt) {
    let enter = fromCharCode(13)
    let bs = fromCharCode(127)
    let tab = fromCharCode(9)
    let esc = fromCharCode(27)
    if kc == 36 { emit enter }          // Return
    if kc == 22 { emit bs }             // Backspace
    if kc == 23 { emit tab }            // Tab
    if kc == 9  { emit esc }            // Escape
    // arrows -> ANSI CSI
    if kc == 111 { emit esc + "[A" }    // Up
    if kc == 116 { emit esc + "[B" }    // Down
    if kc == 114 { emit esc + "[C" }    // Right
    if kc == 113 { emit esc + "[D" }    // Left
    // navigation / editing keys
    if kc == 110 { emit esc + "[H" }    // Home
    if kc == 115 { emit esc + "[F" }    // End
    if kc == 112 { emit esc + "[5~" }   // Page Up
    if kc == 117 { emit esc + "[6~" }   // Page Down
    if kc == 118 { emit esc + "[2~" }   // Insert
    if kc == 119 { emit esc + "[3~" }   // Delete
    // function keys F1-F12 (xterm: F1-F4 = SS3, F5-F12 = CSI ~)
    if kc == 67 { emit esc + "OP" }     // F1
    if kc == 68 { emit esc + "OQ" }     // F2
    if kc == 69 { emit esc + "OR" }     // F3
    if kc == 70 { emit esc + "OS" }     // F4
    if kc == 71 { emit esc + "[15~" }   // F5
    if kc == 72 { emit esc + "[17~" }   // F6
    if kc == 73 { emit esc + "[18~" }   // F7
    if kc == 74 { emit esc + "[19~" }   // F8
    if kc == 75 { emit esc + "[20~" }   // F9
    if kc == 76 { emit esc + "[21~" }   // F10
    if kc == 95 { emit esc + "[23~" }   // F11
    if kc == 96 { emit esc + "[24~" }   // F12
    // printable via keyChar layout (shared shape with cortex)
    let ch = kCharOf(kc, shift)
    if ch != "" {
        if ctrl == 1 {                  // Ctrl-A..Ctrl-Z -> 0x01..0x1A
            let cc = toInt(charCode(ch))
            if cc >= 97 && cc <= 122 { emit fromCharCode((cc - 96) + "") }
        }
        if alt == 1 { emit esc + ch }   // Alt/Meta -> ESC-prefixed (word-move, etc.)
        emit ch
    }
    emit ""
}

// keycode -> character for the US layout home/number rows (MVP subset).
func kCharOf(kc, shift) {
    // letters: kc 24..33 = q..p ; 38..46 = a..; 52..58 = z.. ; use a table string.
    let row1 = "qwertyuiop"    // kc 24..33
    let row2 = "asdfghjkl"     // kc 38..46
    let row3 = "zxcvbnm"       // kc 52..58
    let nums = "1234567890"    // kc 10..19
    let symN = "!@#$%^&*()"    // shifted numbers
    if kc >= 24 && kc <= 33 { emit kShiftAlpha(substring(row1, kc - 24, kc - 23), shift) }
    if kc >= 38 && kc <= 46 { emit kShiftAlpha(substring(row2, kc - 38, kc - 37), shift) }
    if kc >= 52 && kc <= 58 { emit kShiftAlpha(substring(row3, kc - 52, kc - 51), shift) }
    if kc >= 10 && kc <= 19 { if shift == 1 { emit substring(symN, kc - 10, kc - 9) }  emit substring(nums, kc - 10, kc - 9) }
    if kc == 65 { emit " " }            // Space
    if kc == 20 { if shift == 1 { emit "_" }  emit "-" }
    if kc == 21 { if shift == 1 { emit "+" }  emit "=" }
    if kc == 51 { if shift == 1 { emit "|" }  emit fromCharCode(92) }   // backslash
    if kc == 47 { if shift == 1 { emit ":" }  emit ";" }
    if kc == 48 { if shift == 1 { emit fromCharCode(34) }  emit "'" }
    if kc == 59 { if shift == 1 { emit "<" }  emit "," }
    if kc == 60 { if shift == 1 { emit ">" }  emit "." }
    if kc == 61 { if shift == 1 { emit "?" }  emit "/" }
    emit ""
}
func kShiftAlpha(c, shift) {
    if shift == 1 { emit toUpper(c) }
    emit c
}

// write a pasted clipboard string to the pty, wrapping it in bracketed-paste
// markers (ESC[200~ … ESC[201~) when the app enabled DECSET 2004 — keeps vim &
// friends from auto-indenting the pasted block.
func kSendPaste(m, clip, meta) {
    if len(clip) <= 0 { emit 0 }
    if gridPasteM(meta) == 1 {
        let e = fromCharCode(27)
        let w = e + "[200~" + clip + e + "[201~"
        fdWrite(m, w, len(w))
    } else {
        fdWrite(m, clip, len(clip))
    }
    emit 0
}

// encode a mouse event for the app: SGR 1006 (ESC[<b;col;row;M/m, msgr==1) or the
// legacy X10 form (ESC[M b+32 col+32 row+32). col/row are 0-based here.
func kMouseSeq(msgr, btn, col, row, press) {
    let e = fromCharCode(27)
    if msgr == 1 {
        let tm = "M"  if press == 0 { tm = "m" }
        emit e + "[<" + btn + ";" + (col + 1) + ";" + (row + 1) + tm
    }
    let cc = col + 33  if cc > 255 { cc = 255 }
    let rr = row + 33  if rr > 255 { rr = 255 }
    let lb = btn  if press == 0 { lb = 3 }     // legacy: any release = button 3
    emit e + "[M" + fromCharCode(lb + 32) + fromCharCode(cc) + fromCharCode(rr)
}

// xterm-256 colour index (0..255) -> packed 0xRRGGBB.
func xterm256rgb(idx) {
    // base-16: One Dark palette (readable on a dark bg — the VGA defaults made
    // ANSI blue 0x000080 etc. unreadable). black raised to a dark grey so black
    // text isn't invisible.
    if idx == 0  { emit 3883080 }    if idx == 1  { emit 14707829 }  // black(grey), red
    if idx == 2  { emit 10011513 }   if idx == 3  { emit 15057019 }  // green, yellow
    if idx == 4  { emit 6402031 }    if idx == 5  { emit 13007069 }  // blue, magenta
    if idx == 6  { emit 5682882 }    if idx == 7  { emit 11252415 }  // cyan, white
    if idx == 8  { emit 6054768 }    if idx == 9  { emit 14707829 }  // br.grey, br.red
    if idx == 10 { emit 10011513 }   if idx == 11 { emit 15057019 }  // br.green, br.yellow
    if idx == 12 { emit 6402031 }    if idx == 13 { emit 13007069 }  // br.blue, br.magenta
    if idx == 14 { emit 5682882 }    if idx == 15 { emit 16777215 }  // br.cyan, br.white
    if idx >= 232 { let v = 8 + (idx - 232) * 10  emit v * 65536 + v * 256 + v }  // greyscale ramp
    let n = idx - 16                  // 6x6x6 colour cube
    let r = n / 36  let g = (n - r * 36) / 6  let b = n - r * 36 - g * 6
    let rv = 0  if r > 0 { rv = 55 + r * 40 }
    let gv = 0  if g > 0 { gv = 55 + g * 40 }
    let bv = 0  if b > 0 { bv = 55 + b * 40 }
    emit rv * 65536 + gv * 256 + bv
}
// ── mutable 256-colour palette (OSC 4 can override entries) ──────────────────
// 3 bytes (R,G,B) per index. Seeded from xterm256rgb; OSC 4 rewrites entries.
func palInit() {
    let pal = bufNew(768)
    let i = 0
    while i < 256 {
        let rgb = xterm256rgb(i)
        let r = rgb / 65536  let g = (rgb - r * 65536) / 256  let b = rgb - r * 65536 - g * 256
        bufSetByte(pal, i * 3, r)  bufSetByte(pal, i * 3 + 1, g)  bufSetByte(pal, i * 3 + 2, b)
        i = i + 1
    }
    emit pal
}
func palRGB(pal, idx) {
    let o = idx * 3
    emit toInt(bufGetByte(pal, o)) * 65536 + toInt(bufGetByte(pal, o + 1)) * 256 + toInt(bufGetByte(pal, o + 2))
}
func palSet(pal, idx, rgb) {
    if idx < 0 || idx > 255 { emit 0 }
    let r = rgb / 65536  let g = (rgb - r * 65536) / 256  let b = rgb - r * 65536 - g * 256
    bufSetByte(pal, idx * 3, r)  bufSetByte(pal, idx * 3 + 1, g)  bufSetByte(pal, idx * 3 + 2, b)
    emit 0
}

func _isHexCh(c) { if (c >= 48 && c <= 57) || (c >= 97 && c <= 102) || (c >= 65 && c <= 70) { emit 1 }  emit 0 }

// parse an OSC colour spec ("#RRGGBB", "#RRRRGGGGBBBB", "rgb:rr/gg/bb") -> rgb, -1 if not.
func parseOscColor(spec) {
    let h = indexOf(spec, "#")
    if h >= 0 {
        let raw = substring(spec, h + 1, len(spec))
        let e = 0
        while e < len(raw) && _isHexCh(toInt(charCode(substring(raw, e, e + 1)))) == 1 { e = e + 1 }
        let hex = substring(raw, 0, e)
        if len(hex) >= 12 { emit hexColor(substring(hex, 0, 2) + substring(hex, 4, 6) + substring(hex, 8, 10), 0) }
        if len(hex) >= 6 { emit hexColor(substring(hex, 0, 6), 0) }
        emit 0 - 1
    }
    let r = indexOf(spec, "rgb:")
    if r >= 0 {
        let body = substring(spec, r + 4, len(spec))
        let p1 = indexOf(body, "/")
        if p1 < 0 { emit 0 - 1 }
        let cr = substring(body, 0, p1)
        let rest = substring(body, p1 + 1, len(body))
        let p2 = indexOf(rest, "/")
        if p2 < 0 { emit 0 - 1 }
        let cg = substring(rest, 0, p2)
        let cb = substring(rest, p2 + 1, len(rest))
        emit hexColor(substring(cr, 0, 2) + substring(cg, 0, 2) + substring(cb, 0, 2), 0)
    }
    emit 0 - 1
}

// scan a chunk for OSC 4 / 10 / 11 / 12; apply OSC 4 to `pal` (in place); return
// "fg,bg,cursor" rgb ints from the last 10/11/12 (-1 = unchanged).
func oscApply(s, pal) {
    let fgC = 0 - 1  let bgC = 0 - 1  let curC = 0 - 1
    let n = len(s)
    let i = 0
    while i + 1 < n {
        if toInt(charCode(substring(s, i, i + 1))) == 27 && toInt(charCode(substring(s, i + 1, i + 2))) == 93 {
            let bstart = i + 2
            let j = bstart
            let term = 0
            while j < n && term == 0 {
                let cj = toInt(charCode(substring(s, j, j + 1)))
                if cj == 7 { term = 1 }
                else { if cj == 27 && j + 1 < n && toInt(charCode(substring(s, j + 1, j + 2))) == 92 { term = 1 }
                else { j = j + 1 } }
            }
            let body = substring(s, bstart, j)
            if j < n && toInt(charCode(substring(s, j, j + 1))) == 7 { i = j + 1 } else { i = j + 2 }
            let semi = indexOf(body, ";")
            if semi > 0 {
                let ps = substring(body, 0, semi)
                let rest = substring(body, semi + 1, len(body))
                if ps == "10" { let c = parseOscColor(rest)  if c >= 0 { fgC = c } }
                if ps == "11" { let c = parseOscColor(rest)  if c >= 0 { bgC = c } }
                if ps == "12" { let c = parseOscColor(rest)  if c >= 0 { curC = c } }
                if ps == "4" {
                    let r2 = rest  let go = 1
                    while go == 1 {
                        let s1 = indexOf(r2, ";")
                        if s1 < 0 { go = 0 }
                        else {
                            let idx = toInt(substring(r2, 0, s1))
                            let after = substring(r2, s1 + 1, len(r2))
                            let s2 = indexOf(after, ";")
                            let colStr = after
                            if s2 >= 0 { colStr = substring(after, 0, s2)  r2 = substring(after, s2 + 1, len(after)) }
                            else { go = 0 }
                            let col = parseOscColor(colStr)
                            if col >= 0 { palSet(pal, idx, col) }
                        }
                    }
                }
            }
        } else { i = i + 1 }
    }
    emit fgC + "," + bgC + "," + curC
}

// Scan a chunk for terminal QUERIES the program waits on, and return the bytes to
// write back to the pty. Without these, fish/zsh/ncurses stall before the prompt:
//   ESC[6n  DSR cursor position -> ESC[<row>;<col>R
//   ESC[c / ESC[0c  DA1 -> ESC[?1;2c   ;   ESC[>c  DA2 -> ESC[>1;10;0c
func termReplies(s, meta, cols, rows) {
    let e = fromCharCode(27)
    let reply = ""
    let n = len(s)
    let i = 0
    while i + 1 < n {
        if toInt(charCode(substring(s, i, i + 1))) == 27 && toInt(charCode(substring(s, i + 1, i + 2))) == 91 {
            let j = i + 2
            let params = ""
            let fin = 0
            while j < n && fin == 0 {
                let cj = toInt(charCode(substring(s, j, j + 1)))
                if cj >= 64 && cj <= 126 { fin = cj }
                else { params = params + substring(s, j, j + 1) }
                j = j + 1
            }
            if fin == 110 && params == "6" {                       // DSR cursor position
                let cur = gridCursorM(meta)
                let cm = indexOf(cur, ",")
                let cr = toInt(substring(cur, 0, cm)) + 1
                let cc = toInt(substring(cur, cm + 1, len(cur))) + 1
                if cr < 1 || cr > rows { cr = 1 }
                if cc < 1 || cc > cols { cc = 1 }
                reply = reply + e + "[" + cr + ";" + cc + "R"
            }
            if fin == 99 {                                         // DA
                if params == "" || params == "0" { reply = reply + e + "[?1;2c" }       // DA1
                else { if substring(params, 0, 1) == ">" { reply = reply + e + "[>1;10;0c" } }  // DA2
            }
            if fin == 116 {                                        // 't' window manipulation
                if params == "18" { reply = reply + e + "[8;" + rows + ";" + cols + "t" }       // text-area size (chars)
                if params == "14" { reply = reply + e + "[4;" + (rows * 16) + ";" + (cols * 8) + "t" }  // size (px)
            }
            i = j
        } else { i = i + 1 }
    }
    emit reply
}

// attr/battr cell byte -> rgb. <=1 = default (use `def`); >=2 = palette[byte-2].
func kColorOf(byteVal, def, pal) {
    if byteVal <= 1 { emit def }
    emit palRGB(pal, byteVal - 2)
}

// parse a hex colour string ("CCFFCC" / "#CCFFCC" / "0xCCFFCC") -> 0xRRGGBB; def if blank.
func hexColor(s, def) {
    if s == "" { emit def }
    let h = s
    if len(h) >= 2 && substring(h, 0, 2) == "0x" { h = substring(h, 2, len(h)) }
    if len(h) >= 1 && substring(h, 0, 1) == "#" { h = substring(h, 1, len(h)) }
    let v = 0
    let i = 0
    while i < len(h) {
        let c = toInt(charCode(substring(h, i, i + 1)))
        let d = 0 - 1
        if c >= 48 && c <= 57 { d = c - 48 }
        if c >= 97 && c <= 102 { d = c - 87 }
        if c >= 65 && c <= 70 { d = c - 55 }
        if d >= 0 { v = v * 16 + d }
        i = i + 1
    }
    emit v
}

// strip trailing spaces from one line (terminal selection copies rtrimmed rows).
func rtrimLine(s) {
    let n = len(s)
    while n > 0 && substring(s, n - 1, n) == " " { n = n - 1 }
    emit substring(s, 0, n)
}

// row-major slice of one line from column a (inclusive) to b (exclusive), clamped.
func kRowSlice(line, a, b) {
    let ln = len(line)
    let lo = a   if lo < 0 { lo = 0 }   if lo > ln { lo = ln }
    let hi = b   if hi < 0 { hi = 0 }   if hi > ln { hi = ln }
    if hi <= lo { emit "" }
    emit substring(line, lo, hi)
}

// extract selected text from the live grid for range (sr,sc)..(er,ec), inclusive
// of the end cell. Multi-row joins with '\n'; each row is rtrimmed.
func kSelText(gb, cols, rows, sr, sc, er, ec) {
    let r1 = sr  let c1 = sc  let r2 = er  let c2 = ec
    if r1 > r2 || (r1 == r2 && c1 > c2) {     // normalize so (r1,c1) precedes (r2,c2)
        r1 = er  c1 = ec  r2 = sr  c2 = sc
    }
    let text = gridPlainB(gb, cols, rows)
    if r1 == r2 { emit rtrimLine(kRowSlice(getLine(text, r1), c1, c2 + 1)) }
    let out = rtrimLine(kRowSlice(getLine(text, r1), c1, cols))
    let r = r1 + 1
    while r < r2 {
        out = out + fromCharCode(10) + rtrimLine(getLine(text, r))
        r = r + 1
    }
    out = out + fromCharCode(10) + rtrimLine(kRowSlice(getLine(text, r2), 0, c2 + 1))
    emit out
}

// is cell (r,c) inside the inclusive linear selection (sr,sc)..(er,ec)?
func kInSel(r, c, cols, sr, sc, er, ec) {
    let r1 = sr  let c1 = sc  let r2 = er  let c2 = ec
    if r1 > r2 || (r1 == r2 && c1 > c2) { r1 = er  c1 = ec  r2 = sr  c2 = sc }
    let pos = r * cols + c
    let lo = r1 * cols + c1
    let hi = r2 * cols + c2
    if pos >= lo && pos <= hi { emit 1 }
    emit 0
}

// UTF-8: byte count of a sequence from its lead byte, and codepoint decode.
// The grid is one CELL per column but a cell may hold a multibyte glyph, so the
// renderer must walk the row by character (not byte) to keep columns aligned.
func kUtf8Len(b) {
    if b < 128 { emit 1 }
    if b < 224 { emit 2 }
    if b < 240 { emit 3 }
    emit 4
}
func kUtf8Cp(s, i, cl) {
    let b0 = toInt(charCode(substring(s, i, i + 1)))
    if cl == 1 { emit b0 }
    if cl == 2 {
        let b1 = toInt(charCode(substring(s, i + 1, i + 2)))
        emit (b0 - 192) * 64 + (b1 - 128)
    }
    if cl == 3 {
        let b1 = toInt(charCode(substring(s, i + 1, i + 2)))
        let b2 = toInt(charCode(substring(s, i + 2, i + 3)))
        emit (b0 - 224) * 4096 + (b1 - 128) * 64 + (b2 - 128)
    }
    let c1 = toInt(charCode(substring(s, i + 1, i + 2)))
    let c2 = toInt(charCode(substring(s, i + 2, i + 3)))
    let c3 = toInt(charCode(substring(s, i + 3, i + 4)))
    emit (b0 - 240) * 262144 + (c1 - 128) * 4096 + (c2 - 128) * 64 + (c3 - 128)
}
// decode the codepoint stored in a grid cell (buffer gb, byte offset off, width w).
func kCellCp(gb, off, w) {
    let b0 = toInt(bufGetByte(gb, off + 1))
    if w == 1 { emit b0 }
    if w == 2 { emit (b0 - 192) * 64 + (toInt(bufGetByte(gb, off + 2)) - 128) }
    if w == 3 { emit (b0 - 224) * 4096 + (toInt(bufGetByte(gb, off + 2)) - 128) * 64 + (toInt(bufGetByte(gb, off + 3)) - 128) }
    let c1 = toInt(bufGetByte(gb, off + 2))  let c2 = toInt(bufGetByte(gb, off + 3))  let c3 = toInt(bufGetByte(gb, off + 4))
    emit (b0 - 240) * 262144 + (c1 - 128) * 4096 + (c2 - 128) * 64 + (c3 - 128)
}

// codepoint of the col-th character in a UTF-8 line (32 = space if past the end)
func kCharAtCol(line, col) {
    let n = len(line)
    let bi = 0
    let c = 0
    while bi < n {
        let lead = toInt(charCode(substring(line, bi, bi + 1)))
        let cl = kUtf8Len(lead)
        if bi + cl > n { cl = n - bi }
        if c == col { emit kUtf8Cp(line, bi, cl) }
        bi = bi + cl
        c = c + 1
    }
    emit 32
}

// ── render: stem grid -> framebuffer, per-cell ANSI colour. Block cursor; bell. ──
func kDrawScreen(px, W, H, font, gb, ab, bb, ub, meta, cols, rows, bg, fg, bell, curColor, curStyle, hasSel, selSR, selSC, selER, selEC, pal, yBase) {
    let back = bg
    if bell == 1 { back = 3355494 }                 // visual-bell flash
    fbClear(px, W, H, back)
    let total = cols * rows
    // read every cell straight from the plane buffers — no per-frame string alloc.
    let r = 0
    while r < rows {
        let c = 0
        while c < cols {
            let idx = r * cols + c
            let off = idx * 5
            let x = 4 + c * 8  let y = yBase + r * 16
            let cellFg = kColorOf(toInt(bufGetByte(ab, idx)), fg, pal)
            let cellBg = kColorOf(toInt(bufGetByte(bb, idx)), back, pal)
            if hasSel == 1 && kInSel(r, c, cols, selSR, selSC, selER, selEC) == 1 {
                cellBg = 3756378            // 0x395A5A selection highlight
            }
            if cellBg != back { fbFillRect(px, W, x, y, 8, 16, cellBg) }
            let w = toInt(bufGetByte(gb, off))
            let chcode = kCellCp(gb, off, w)
            if chcode != 32 { stemDrawChar(px, W, H, font, x, y, chcode, cellFg) }
            if toInt(bufGetByte(ub, idx)) == 1 { fbFillRect(px, W, x, y + 14, 8, 1, cellFg) }   // SGR 4 underline
            c = c + 1
        }
        r = r + 1
    }
    // block cursor (inverted cell) at gridCursor row,col
    let cur = gridCursorM(meta)
    let comma = indexOf(cur, ",")
    let cr = toInt(substring(cur, 0, comma))
    let cc = toInt(substring(cur, comma + 1, len(cur)))
    if cr >= 0 && cr < rows && cc >= 0 && cc < cols {
        let cx = 4 + cc * 8  let cy = yBase + cr * 16
        let coff = (cr * cols + cc) * 5
        let cglyph = kCellCp(gb, coff, toInt(bufGetByte(gb, coff)))   // cursor cell glyph (from buffer)
        let cstyle = curStyle                            // app DECSCUSR overrides the config default
        let appShape = gridCshapeM(meta)
        if appShape == 1 { cstyle = 1 }                 // bar
        if appShape == 2 { cstyle = 0 }                 // block
        if appShape == 3 { cstyle = 2 }                 // underline
        if cstyle == 1 {                                // bar: 2px at cell left, glyph normal
            fbFillRect(px, W, cx, cy, 2, 16, curColor)
            if cglyph != 32 { stemDrawChar(px, W, H, font, cx, cy, cglyph, fg) }
        } else {
            if cstyle == 2 {                            // underline: 2px at cell bottom, glyph normal
                fbFillRect(px, W, cx, cy + 14, 8, 2, curColor)
                if cglyph != 32 { stemDrawChar(px, W, H, font, cx, cy, cglyph, fg) }
            } else {                                    // block: fill cell, glyph inverted
                fbFillRect(px, W, cx, cy, 8, 16, curColor)
                if cglyph != 32 { stemDrawChar(px, W, H, font, cx, cy, cglyph, back) }
            }
        }
    }
}

// scrollback view: history + live grid, offset up by scrollOff lines (monochrome —
// scrolled-off rows are stored as plain text). A "▲" marks we're not at the bottom.
func kDrawScrollback(px, W, H, font, scrollback, gb, cols, rows, scrollOff, bg, fg, yBase) {
    fbClear(px, W, H, bg)
    let view = gridScrollView(scrollback, gridPlainB(gb, cols, rows), rows, scrollOff)
    let r = 0
    while r < rows {
        let line = gridStripSgr(getLine(view, r))   // history rows carry SGR; strip for the mono view
        fbDrawText(px, W, H, font, 4, yBase + r * 16, line, fg)
        r = r + 1
    }
    fbDrawText(px, W, H, font, W - 80, yBase, "^" + scrollOff + " (shift+pgdn=back)", 16776960)   // scroll indicator
}

// the tab bar across row 0: one segment per tab (number + short title), the active
// one highlighted in krypton green. Drawn over the top 18px after the grid render.
func kDrawTabBar(px, W, H, font, sessions, tabCount, active) {
    fbFillRect(px, W, 0, 0, W, 18, 858384)             // 0x0d1510 bar background
    let segW = W / tabCount
    if segW > 200 { segW = 200 }
    let i = 0
    while i < tabCount {
        let x0 = i * segW
        let s = envGet(sessions, "" + i)
        let t = envGet(s, "title")
        let label = "" + (i + 1) + " " + t
        let maxCh = (segW - 16) / 8
        if maxCh < 1 { maxCh = 1 }
        if len(label) > maxCh { label = substring(label, 0, maxCh) }
        let txt = 10010008                              // 0x98ff98 light green
        if i == active {
            fbFillRect(px, W, x0, 0, segW - 1, 18, 3841354)   // 0x3a9d4a active green
            txt = 858384                                // dark text on the highlight
        } else {
            fbFillRect(px, W, x0, 0, segW - 1, 18, 1976352)   // 0x1e2a20 inactive
        }
        fbDrawText(px, W, H, font, x0 + 6, 1, label, txt)
        i = i + 1
    }
}

// ── session model: each tab is one PTY + its own grid plane buffers + meta. The
// per-tab state lives in an env (envSet/envGet) since native lists can't index. ──
func sessNew(cols, rows, shell, term) {
    let m = ptyMaster("/dev/ptmx")
    let slave = ptySlaveName(m)
    let nl = fromCharCode(10)
    let wrapPath = "/tmp/.stem-shell-" + m
    let wrap = "#!/bin/sh" + nl + "export TERM=" + term + nl + "export COLORTERM=truecolor" + nl + "exec " + shell + nl
    writeFile(wrapPath, wrap)
    exec("chmod +x " + wrapPath + " 2>/dev/null")
    let pid = ptyForkExec(slave, wrapPath)
    let tries = 0
    while tries < 60 { if ptySetSize(m, rows, cols) == 0 { tries = 60 } else { sleepUs(0, 10000)  tries = tries + 1 } }
    ptySetNonblock(m)
    let gb = bufNew(5 * cols * rows)  let ab = bufNew(cols * rows)  let bb = bufNew(cols * rows)  let ub = bufNew(cols * rows)
    gridBlank(gb, ab, bb, ub, cols, rows)
    let s = envNew()
    s = envSet(s, "m", m)        s = envSet(s, "pid", pid)
    s = envSet(s, "gb", gb)      s = envSet(s, "ab", ab)      s = envSet(s, "bb", bb)      s = envSet(s, "ub", ub)
    s = envSet(s, "meta", gridInitMeta())  s = envSet(s, "sb", "")  s = envSet(s, "pending", "")  s = envSet(s, "title", "stem")
    s = envSet(s, "scrollOff", 0)  s = envSet(s, "bell", 0)  s = envSet(s, "quiet", 0)
    s = envSet(s, "kicked", 0)  s = envSet(s, "kickIn", 30)  s = envSet(s, "kickArmed", 1)
    s = envSet(s, "scrollback", "")  s = envSet(s, "selecting", 0)  s = envSet(s, "hasSel", 0)
    s = envSet(s, "selSR", 0)  s = envSet(s, "selSC", 0)  s = envSet(s, "selER", 0)  s = envSet(s, "selEC", 0)
    emit s
}

// drain one session's PTY, feed its grid buffers (in place), update its env. Sets
// the "dirty" field to 1 if the session's screen changed this pump. Returns the
// updated session env. Same logic for foreground + background tabs.
func sessPump(s, cols, rows, pal, sbCap, bellMode) {
    let m = envGet(s, "m")
    let gb = envGet(s, "gb")  let ab = envGet(s, "ab")  let bb = envGet(s, "bb")  let ub = envGet(s, "ub")
    let meta = envGet(s, "meta")  let pending = envGet(s, "pending")  let sb = envGet(s, "scrollback")
    let title = envGet(s, "title")  let bell = envGet(s, "bell")  let quiet = envGet(s, "quiet")
    let kicked = envGet(s, "kicked")  let kickIn = envGet(s, "kickIn")  let kickArmed = envGet(s, "kickArmed")
    let dirty = 0
    if kickArmed == 1 && kickIn > 0 {
        kickIn = kickIn - 1
        if kickIn == 0 { fdWrite(m, fromCharCode(12), 1)  kicked = 1  kickArmed = 0  dirty = 1 }
    }
    let out = fdRead(m, 16384)
    if len(out) > 0 {
        if kickArmed == 1 { kickIn = 30 }
        let chunk = pending + out
        let nt = oscTitle(chunk, title)
        if nt != title && nt != "" { title = nt }
        let cut = gridSafeLen(chunk)
        if cut > 0 {
            let fed = substring(chunk, 0, cut)
            oscApply(fed, pal)                       // palette (OSC 4) is shared across tabs
            meta = gridFeedB(gb, ab, bb, ub, meta, fed, cols, rows)
            let rep = termReplies(fed, meta, cols, rows)
            if len(rep) > 0 { fdWrite(m, rep, len(rep)) }
            if gridBellM(meta) == 1 { if bellMode == "visual" { bell = 1 } }
            let sc = gridScrolledM(meta)
            if len(sc) > 0 {
                sb = sb + sc
                let cap = sbCap * 200
                if len(sb) > cap { sb = substring(sb, len(sb) - cap, len(sb)) }
            }
            dirty = 1
        }
        pending = substring(chunk, cut, len(chunk))
        quiet = 0
    } else {
        quiet = quiet + 1
        if quiet >= 2 && len(pending) > 0 { meta = gridFeedB(gb, ab, bb, ub, meta, pending, cols, rows)  pending = ""  dirty = 1 }
    }
    s = envSet(s, "meta", meta)  s = envSet(s, "pending", pending)  s = envSet(s, "scrollback", sb)
    s = envSet(s, "title", title)  s = envSet(s, "bell", bell)  s = envSet(s, "quiet", quiet)
    s = envSet(s, "kicked", kicked)  s = envSet(s, "kickIn", kickIn)  s = envSet(s, "kickArmed", kickArmed)
    s = envSet(s, "dirty", dirty)
    emit s
}

just run {
    // ── config ──
    let conf = confLoad()
    let shell = confGet(conf, "shell", "/bin/bash")
    let term = confGet(conf, "term", "xterm-256color")          // $TERM for the child
    let bg = hexColor(confGet(conf, "bg", ""), 1054753)         // 0x101821 dark
    let fg = hexColor(confGet(conf, "fg", ""), 13434828)        // 0xCCFFCC soft green
    let curColor = hexColor(confGet(conf, "cursor_color", ""), fg)
    let curStyleS = confGet(conf, "cursor_style", "block")      // block | bar | underline
    let curStyle = 0
    if curStyleS == "bar" { curStyle = 1 }
    if curStyleS == "underline" { curStyle = 2 }
    let bellMode = confGet(conf, "bell", "visual")             // visual | off

    // initial size from config grid (a tiling compositor may override on map).
    let cfgCols = confGetInt(conf, "cols", 100)
    let cfgRows = confGetInt(conf, "rows", 30)
    if cfgCols < 20 { cfgCols = 20 }   if cfgCols > 400 { cfgCols = 400 }
    if cfgRows < 5  { cfgRows = 5 }    if cfgRows > 200 { cfgRows = 200 }
    let W = cfgCols * 8 + 8  let H = cfgRows * 16 + 4
    let cols = (W - 8) / 8
    let rows = (H - 4) / 16

    // reserve the top cell-row for the tab bar -> terminal grid is `trows` tall.
    let trows = rows - 1  if trows < 1 { trows = 1 }
    // ── session 0 created BEFORE wlConnect so the forked shell doesn't inherit
    // the wayland fd (which would keep orphan windows alive). sessNew forks the
    // PTY (TERM wrapper), allocs+blanks the planes, returns a session env. ──
    let sessions = envNew()
    sessions = envSet(sessions, "0", sessNew(cols, trows, shell, term))
    let tabCount = 1  let active = 0

    // ── wayland surface (child already forked: it has no Wayland fd) ──
    let fd = wlConnect()
    if fd < 0 { print("stem: wayland connect failed")  exit("1") }
    let REG = 2  let COMP = 3  let SHM = 4  let WM = 5  let SEAT = 6  let KB = 7  let PTR = 8
    let SURF = 9  let XS = 10  let TOP = 11   // sequential object ids, NO gaps
    wlGetRegistry(fd, REG)
    let rb = bufNew(8192)
    let rn = wlRecvInto(fd, rb, 8192)
    wlBind(fd, REG, _wlFind(rb, rn, "wl_compositor"), "wl_compositor", 4, COMP)
    wlBind(fd, REG, _wlFind(rb, rn, "wl_shm"), "wl_shm", 1, SHM)
    wlBind(fd, REG, _wlFind(rb, rn, "xdg_wm_base"), "xdg_wm_base", 1, WM)
    wlBind(fd, REG, _wlFind(rb, rn, "wl_seat"), "wl_seat", 5, SEAT)
    wlGetKeyboard(fd, SEAT, KB)
    wlGetPointer(fd, SEAT, PTR)
    wlCreateSurface(fd, COMP, SURF)
    wlGetXdgSurface(fd, WM, XS, SURF)
    wlGetToplevel(fd, XS, TOP)
    wlSetTitle(fd, TOP, "stem")
    wlSetAppId(fd, TOP, "stem")
    wlCommit(fd, SURF)

    let font = stemFontLoad()
    fdSetNonblock(fd)                  // wayland fd: poll, don't block
    let pal = palInit()                // live 256-colour palette (OSC 4 overrides)

    let shift = 0  let ctrl = 0  let alt = 0
    let nextId = 12  let prevBuf = 0
    let dirty = 1  let running = 1  let configured = 0
    let lastTitle = "stem"
    let sbCap = confGetInt(conf, "scrollback", 2000)
    let ptrR = 0   let ptrC = 0   let heldBtn = 0 - 1   // SGR button held for drag reporting (-1 none)
    let eb = bufNew(8192)              // reused every loop (was leaking 8KB/iteration)
    let fbMem = 0  let fbPx = 0  let fbPool = 0  let fbSz = 0   // shared framebuffer, reused across frames
    let switchTo = 0 - 1  let wantNew = 0  let wantClose = 0     // deferred tab ops (applied after events)
    let wantResize = 0  let wantW = 0  let wantH = 0
    while running == 1 {
        switchTo = 0 - 1  wantNew = 0  wantClose = 0  wantResize = 0   // reset deferred ops each frame
        // load the ACTIVE tab's per-session state into working locals for this frame
        let act = envGet(sessions, "" + active)
        let m = envGet(act, "m")
        let gb = envGet(act, "gb")  let ab = envGet(act, "ab")  let bb = envGet(act, "bb")  let ub = envGet(act, "ub")
        let meta = envGet(act, "meta")  let scrollback = envGet(act, "scrollback")  let bell = envGet(act, "bell")
        let scrollOff = envGet(act, "scrollOff")
        let selecting = envGet(act, "selecting")  let hasSel = envGet(act, "hasSel")
        let selSR = envGet(act, "selSR")  let selSC = envGet(act, "selSC")  let selER = envGet(act, "selER")  let selEC = envGet(act, "selEC")
        // 1) drain wayland events (non-blocking)
        let en = wlRecvInto(fd, eb, 8192)
        let off = 0
        while off + 8 <= en {
            let obj = wlObject(eb, off)  let op = wlOpcode(eb, off)  let s = wlSize(eb, off)
            if s < 8 { off = en }
            else {
                if obj == WM && op == 0 { wlPong(fd, WM, wlU32(eb, off + 8)) }
                if obj == XS && op == 0 {
                    wlAckConfigure(fd, XS, wlU32(eb, off + 8))   // sessNew arms each tab's kick
                    configured = 1  dirty = 1
                }
                if obj == TOP && op == 0 {                       // resize -> apply to all tabs after events
                    let cw = wlU32(eb, off + 8)  let chh = wlU32(eb, off + 12)
                    if cw > 0 && chh > 0 && (cw != W || chh != H) { wantW = cw  wantH = chh  wantResize = 1 }
                }
                if obj == TOP && op == 1 { running = 0 }    // xdg_toplevel.close -> exit cleanly (keybind close works)
                if obj == 1 && op == 0 { running = 0 }      // wl_display.error
                if obj == KB && op == 4 {                    // wl_keyboard.modifiers (authoritative)
                    let mods = wlU32(eb, off + 12)           // mods_depressed bitmask
                    shift = 0  if bitAnd(mods, 1) != 0 { shift = 1 }   // bit0 = shift
                    ctrl = 0   if bitAnd(mods, 4) != 0 { ctrl = 1 }    // bit2 = ctrl
                    alt = 0    if bitAnd(mods, 8) != 0 { alt = 1 }     // bit3 = mod1 (Alt/Meta)
                }
                if obj == KB && op == 3 {                    // wl_keyboard.key
                    let state = wlU32(eb, off + 20)
                    let kc = wlKeyToKc(wlU32(eb, off + 16))
                    if state == 1 {
                        // ── tab management (handled here, never sent to the shell) ──
                        let tabKey = 0
                        if ctrl == 1 && shift == 1 && kc == 28 { wantNew = 1  tabKey = 1 }        // Ctrl-Shift-T new
                        if ctrl == 1 && shift == 1 && kc == 25 { wantClose = 1  tabKey = 1 }      // Ctrl-Shift-W close
                        if ctrl == 1 && shift == 0 && kc == 112 { switchTo = active - 1  tabKey = 1 }  // Ctrl-PgUp prev
                        if ctrl == 1 && shift == 0 && kc == 117 { switchTo = active + 1  tabKey = 1 }  // Ctrl-PgDn next
                        if alt == 1 && kc >= 10 && kc <= 18 { switchTo = kc - 10  tabKey = 1 }    // Alt-1..9 jump
                        if tabKey == 0 {
                        if hasSel == 1 { hasSel = 0  dirty = 1 }     // typing clears the selection
                        // Shift+PageUp/Down scrolls the scrollback (not sent to shell).
                        if shift == 1 && kc == 112 {
                            scrollOff = scrollOff + trows - 2
                            let maxOff = toInt(lineCount(scrollback))
                            if scrollOff > maxOff { scrollOff = maxOff }
                            dirty = 1
                        }
                        else { if shift == 1 && kc == 117 {
                            scrollOff = scrollOff - (trows - 2)
                            if scrollOff < 0 { scrollOff = 0 }
                            dirty = 1
                        } else {
                            // paste: Ctrl-Shift-V (kc 55) or Shift-Insert (kc 118)
                            let isPaste = 0
                            if ctrl == 1 && shift == 1 && kc == 55 { isPaste = 1 }
                            if shift == 1 && kc == 118 { isPaste = 1 }
                            if isPaste == 1 {
                                let clip = exec("wl-paste -n 2>/dev/null")
                                kSendPaste(m, clip, meta)
                                if scrollOff != 0 { scrollOff = 0  dirty = 1 }
                            } else {
                                if scrollOff != 0 { scrollOff = 0  dirty = 1 }   // any other key jumps back to live
                                let bytes = kKeyBytes(kc, shift, ctrl, alt)
                                if bytes != "" { fdWrite(m, bytes, len(bytes)) }
                            }
                        } }
                        }
                    }
                }
                if obj == PTR && op == 2 {                   // wl_pointer.motion -> track cell
                    let px = toInt(wlU32(eb, off + 12)) / 256   // wl_fixed -> px
                    let py = toInt(wlU32(eb, off + 16)) / 256
                    let nC = (px - 4) / 8    if nC < 0 { nC = 0 }  if nC >= cols { nC = cols - 1 }
                    let nR = (py - 18) / 16  if nR < 0 { nR = 0 }  if nR >= trows { nR = trows - 1 }   // -18: below tab bar
                    let moved = 0  if nC != ptrC || nR != ptrR { moved = 1 }
                    ptrC = nC  ptrR = nR
                    if selecting == 1 { selER = ptrR  selEC = ptrC  hasSel = 1  dirty = 1 }
                    if moved == 1 && heldBtn >= 0 && shift == 0 {   // drag while app tracks the mouse
                        let amM = gridMouseM(meta)
                        let amcM = indexOf(amM, ",")
                        if toInt(substring(amM, 0, amcM)) >= 2 {    // button-drag / any-motion tracking
                            let seq = kMouseSeq(toInt(substring(amM, amcM + 1, len(amM))), heldBtn + 32, ptrC, ptrR, 1)
                            fdWrite(m, seq, len(seq))
                        }
                    }
                }
                if obj == PTR && op == 3 {                   // wl_pointer.button
                    let btn = wlU32(eb, off + 16)            // BTN_LEFT = 272
                    let bstate = wlU32(eb, off + 20)         // 1 = pressed
                    let amB = gridMouseM(meta)               // "mlevel,msgr"
                    let amcB = indexOf(amB, ",")
                    let mlevelB = toInt(substring(amB, 0, amcB))
                    let msgrB = toInt(substring(amB, amcB + 1, len(amB)))
                    if mlevelB > 0 && shift == 0 {           // app grabs the mouse (Shift = local override)
                        let mbtn = 0 - 1
                        if btn == 272 { mbtn = 0 }  if btn == 274 { mbtn = 1 }  if btn == 273 { mbtn = 2 }
                        if mbtn >= 0 {
                            let seq = kMouseSeq(msgrB, mbtn, ptrC, ptrR, bstate)
                            fdWrite(m, seq, len(seq))
                            if bstate == 1 { heldBtn = mbtn } else { heldBtn = 0 - 1 }
                        }
                    } else { if btn == 272 {
                        if bstate == 1 {                     // press: anchor selection at cursor cell
                            selecting = 1  hasSel = 0
                            selSR = ptrR  selSC = ptrC  selER = ptrR  selEC = ptrC
                            dirty = 1
                        } else {                             // release: copy selection to clipboard
                            selecting = 0
                            if hasSel == 1 && scrollOff == 0 {
                                let seltext = kSelText(gb, cols, trows, selSR, selSC, selER, selEC)
                                if len(seltext) > 0 {
                                    writeFile("/tmp/.stem_sel", seltext)
                                    exec("wl-copy < /tmp/.stem_sel 2>/dev/null")
                                    exec("wl-copy --primary < /tmp/.stem_sel 2>/dev/null")   // X-style PRIMARY
                                }
                            }
                        }
                    }
                    // middle-click pastes the PRIMARY selection (the Linux idiom)
                    if btn == 274 && bstate == 1 {
                        let clip = exec("wl-paste --primary -n 2>/dev/null")
                        kSendPaste(m, clip, meta)
                        if scrollOff != 0 { scrollOff = 0  dirty = 1 }
                    }
                    }
                }
                if obj == PTR && op == 4 {                   // wl_pointer.axis (scroll wheel)
                    let ax = wlU32(eb, off + 12)             // 0 = vertical
                    if ax == 0 {
                        let msb = toInt(bufGetByte(eb, off + 19))   // sign byte (negative = up)
                        let amW = gridMouseM(meta)
                        let amcW = indexOf(amW, ",")
                        let mlevelW = toInt(substring(amW, 0, amcW))
                        let msgrW = toInt(substring(amW, amcW + 1, len(amW)))
                        if mlevelW > 0 && shift == 0 {       // app wants the wheel (less/vim scroll)
                            let wb = 65  if msb >= 128 { wb = 64 }   // 64 = wheel up, 65 = down
                            let seq = kMouseSeq(msgrW, wb, ptrC, ptrR, 1)
                            fdWrite(m, seq, len(seq))
                        } else {
                            if msb >= 128 {                  // up -> into history
                                scrollOff = scrollOff + 3
                                let maxOff = toInt(lineCount(scrollback))
                                if scrollOff > maxOff { scrollOff = maxOff }
                                dirty = 1
                            } else {                         // down -> toward live
                                scrollOff = scrollOff - 3
                                if scrollOff < 0 { scrollOff = 0 }
                                dirty = 1
                            }
                        }
                    }
                }
                off = off + s
            }
        }
        // ── save the active tab's input-modified state back into its env ──
        act = envSet(act, "scrollOff", scrollOff)  act = envSet(act, "selecting", selecting)  act = envSet(act, "hasSel", hasSel)
        act = envSet(act, "selSR", selSR)  act = envSet(act, "selSC", selSC)  act = envSet(act, "selER", selER)  act = envSet(act, "selEC", selEC)
        act = envSet(act, "bell", bell)
        sessions = envSet(sessions, "" + active, act)

        // ── deferred tab ops ──
        if wantResize == 1 {
            W = wantW  H = wantH  cols = (W - 8) / 8  rows = (H - 4) / 16  trows = rows - 1  if trows < 1 { trows = 1 }
            let ti = 0
            while ti < tabCount {
                let ts = envGet(sessions, "" + ti)  let tm = envGet(ts, "m")
                ptySetSize(tm, trows, cols)
                let ng = bufNew(5 * cols * trows)  let na2 = bufNew(cols * trows)  let nb2 = bufNew(cols * trows)  let nu2 = bufNew(cols * trows)
                gridBlank(ng, na2, nb2, nu2, cols, trows)
                ts = envSet(ts, "gb", ng)  ts = envSet(ts, "ab", na2)  ts = envSet(ts, "bb", nb2)  ts = envSet(ts, "ub", nu2)
                ts = envSet(ts, "meta", gridInitMeta())  ts = envSet(ts, "hasSel", 0)  ts = envSet(ts, "selecting", 0)  ts = envSet(ts, "scrollOff", 0)
                fdWrite(tm, fromCharCode(12), 1)
                sessions = envSet(sessions, "" + ti, ts)
                ti = ti + 1
            }
            dirty = 1
        }
        if wantNew == 1 && tabCount < 12 {
            sessions = envSet(sessions, "" + tabCount, sessNew(cols, trows, shell, term))
            active = tabCount  tabCount = tabCount + 1  dirty = 1
        }
        if switchTo > 0 - 1 {
            let na = switchTo  if na < 0 { na = tabCount - 1 }  if na >= tabCount { na = 0 }
            active = na  dirty = 1
        }
        if wantClose == 1 {
            let cs = envGet(sessions, "" + active)
            exec("kill -9 " + envGet(cs, "pid") + " 2>/dev/null")   // SIGKILL the shell
            fdClose(envGet(cs, "m"))
            if tabCount <= 1 { running = 0 }                        // last tab -> quit
            else {                                                  // splice the tab out + renumber (don't wait on the reap)
                let ci = active
                while ci < tabCount - 1 { sessions = envSet(sessions, "" + ci, envGet(sessions, "" + (ci + 1)))  ci = ci + 1 }
                tabCount = tabCount - 1
                if active >= tabCount { active = tabCount - 1 }
                dirty = 1
            }
        }

        // ── pump every tab (background tabs keep running) ──
        let anyDirty = 0
        let pi = 0
        while pi < tabCount {
            let ps = sessPump(envGet(sessions, "" + pi), cols, trows, pal, sbCap, bellMode)
            if envGet(ps, "dirty") == 1 { anyDirty = 1  if pi == active { dirty = 1 } }
            sessions = envSet(sessions, "" + pi, ps)
            pi = pi + 1
        }

        // ── reap dead children + compact the session list ──
        let newSess = envNew()  let nc = 0  let na3 = active
        let ck = 0
        while ck < tabCount {
            let ks = envGet(sessions, "" + ck)
            if waitChild(envGet(ks, "pid")) == 0 {
                newSess = envSet(newSess, "" + nc, ks)
                if ck == active { na3 = nc }
                nc = nc + 1
            } else { fdClose(envGet(ks, "m"))  if ck == active { na3 = nc } }
            ck = ck + 1
        }
        if nc == 0 { running = 0 }
        else {
            if nc != tabCount { dirty = 1 }
            sessions = newSess  tabCount = nc  active = na3
            if active >= tabCount { active = tabCount - 1 }  if active < 0 { active = 0 }
        }

        // ── reload the active tab for rendering + push its title ──
        act = envGet(sessions, "" + active)
        gb = envGet(act, "gb")  ab = envGet(act, "ab")  bb = envGet(act, "bb")  ub = envGet(act, "ub")
        meta = envGet(act, "meta")  scrollback = envGet(act, "scrollback")  bell = envGet(act, "bell")  scrollOff = envGet(act, "scrollOff")
        selSR = envGet(act, "selSR")  selSC = envGet(act, "selSC")  selER = envGet(act, "selER")  selEC = envGet(act, "selEC")  hasSel = envGet(act, "hasSel")
        let atitle = envGet(act, "title")
        if atitle != lastTitle { lastTitle = atitle  wlSetTitle(fd, TOP, atitle)  wlCommit(fd, SURF) }

        // 4) present. Reuse ONE shared memfd/mmap/pool across frames (mmapShared
        // has no munmap, so a per-frame mapping leaked ~MBs/frame -> OOM crash).
        // Reallocate only when the window grows beyond the current allocation.
        if dirty == 1 && configured == 1 && running == 1 {
            let stride = W * 4
            let sz = stride * H
            if sz > fbSz {
                if fbPool != 0 {
                    if prevBuf != 0 { wlBufferDestroy(fd, prevBuf)  prevBuf = 0 }
                    wlPoolDestroy(fd, fbPool)  sockClose(fbMem)   // old mapping leaks (rare: only on grow)
                }
                fbMem = memfdCreate(sz)
                fbPx = mmapShared(fbMem, sz)
                fbPool = nextId  nextId = nextId + 1
                wlCreatePool(fd, SHM, fbPool, fbMem, sz)
                fbSz = sz
            }
            let px = fbPx
            if scrollOff > 0 { kDrawScrollback(px, W, H, font, scrollback, gb, cols, trows, scrollOff, bg, fg, 18) }
            else { kDrawScreen(px, W, H, font, gb, ab, bb, ub, meta, cols, trows, bg, fg, bell, curColor, curStyle, hasSel, selSR, selSC, selER, selEC, pal, 18) }
            kDrawTabBar(px, W, H, font, sessions, tabCount, active)
            let didFlash = bell  bell = 0
            act = envSet(act, "bell", 0)  sessions = envSet(sessions, "" + active, act)   // consume the flash
            let buf = nextId  nextId = nextId + 1
            wlPoolCreateBuffer(fd, fbPool, buf, 0, W, H, stride, 1)
            wlSurfaceAttach(fd, SURF, buf, 0, 0)
            wlDamage(fd, SURF, 0, 0, W, H)
            wlCommit(fd, SURF)
            if prevBuf != 0 { wlBufferDestroy(fd, prevBuf) }   // free previous frame's lightweight wl_buffer
            prevBuf = buf
            dirty = 0
            if didFlash == 1 { dirty = 1 }   // bell flashed this frame -> redraw normal next frame
        }
        // adaptive poll: fdRead allocates its buffer every call (no GC), so spinning
        // at 8ms while idle is a slow leak. Back off to ~40ms after the shell goes
        // quiet; snap back to 8ms the moment data flows (keeps typing responsive).
        if anyDirty == 0 { sleepUs(0, 40000) } else { sleepUs(0, 8000) }
    }
    // clean shutdown: closing every pty master hangs up its shell (SIGHUP); drop
    // the wayland connection so the surface is destroyed (no orphan window).
    let qi = 0
    while qi < tabCount {
        let qs = envGet(sessions, "" + qi)
        exec("kill -9 " + envGet(qs, "pid") + " 2>/dev/null")   // fish ignores PTY-hangup SIGHUP -> kill explicitly
        fdClose(envGet(qs, "m"))
        qi = qi + 1
    }
    sockClose(fd)
    emit 0
}
